architectury {
    platformSetupLoomIde()
}

classExtensions {
    registerForSourceSet sourceSets.main, "earth.terrarium.botarium.forge.extensions"
}

loom {
    mods {
        testmod {
            sourceSet(sourceSets.test)
        }
    }
}

jar {
    classifier "dev"
    archivesBaseName = "$mod_id-forge-$minecraft_version"
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    compileOnly(project(path: ":common")) { transitive false }
    testCompileOnly(project(path: ":common", configuration: "testApiElements")) { transitive false }
    modRuntimeOnly("curse.maven:energizedpower-782147:4438975")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

sourceSets.main {
    def commonSourceSets = project(":common").sourceSets

    def commonMain = commonSourceSets.named("main")

    java.srcDirs(
            commonMain.map { it.java.srcDirs },
    )

    resources.srcDirs(
            commonMain.map { it.resources.srcDirs },
    )
}

sourceSets.test {
    def commonSourceSets = project(":common").sourceSets
    def commonTest = commonSourceSets.test

    java.srcDirs(
            commonTest.java.srcDirs
    )

    resources.srcDirs(
            commonTest.resources.srcDirs
    )
}

def ENV = System.getenv()

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifact remapJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifactId = rootProject.mod_id + "-" + project.name + "-" + project.minecraft_version
            pom {
                name = 'Botarium'
                description = 'Tiny lil library mod for cross platform energy and registries'
                url = 'https://github.com/terrarium-earth/Botarium'

                scm {
                    connection = 'git:https://github.com/terrarium-earth/Botarium.git'
                    developerConnection = 'git:https://github.com/terrarium-earth/Botarium.git'
                    url = 'https://github.com/terrarium-earth/Botarium'
                }

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            url = "https://maven.resourcefulbees.com/repository/codexadrian/"
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}